s1a: {+/>':.:'x}
s1b: {+/>':+/'3':.:'x}

s2a: */+/.'"forward:{x,0};up:{0,-x};down:{0,x};",/:
s2b: */2#{@[x+y;1;((y@0)*x@2)+]}/.'"forward:{x,0,0};up:{0,0,-x};down:{0,0,x};",/:

s3a: {*/2/'(n;~n:(-2!#x)<(+/.')'+x)}
s3b: {
 in:.''x
 g:*in{ $[1=#x;x;{c:((+y)[x]);c=((#y)%2)<0.5++/c}[y]#x]}/!#*in
 b:*in{ $[1=#x;x;{c:((+y)[x]);~c=((#y)%2)<0.5++/c}[y]#x]}/!#*in
 (2/g)*(2/b)}

s4a: {
 (.''{~(+/(""~)')'x}#5':2_x){
  $[`i=@x;:x;]
  w:{|/5=+/'0>(x,+ x)}
  n:({5 5#{i:y?x;$[^i;y;@[y;y?x;{-1}]]}[x;,/y]}@y)'x
  $[|/w'n;y*+/(0<)#,/*(w')#n;n]}/.*x}

s5a: {
 p:{.'(" "\x)[0 2]}'x
 f:({|/=/x}')#p
 r:,/{(s;e):x@<x;({~|/'x>/:y}[s])#(+!1+e)}'f
 +/1<.#'=r}
s5b: {
 p:{.'(" "\x)[0 2]}'x
 r:,/{
  (s;e):x@<x
  c:|/d:e-s
  (a;b):(1&-1|)'d
  v:(1 -1;0;1 0;0 1;1 1)[1+2/b,a]
  (s+/:v*/:!c),+e}'p
 +/1<.#'=r}

s6: {{(#x)++/x}@({(&0,7,2,x-9){@[x;y;{z;1++/x@y-7,9}[x;y]]}/9+!x-9}@y)@y-1+x}
s6a: {s6[{.*x}'","\*x;80]}
s6b: {s6[{.*x}'","\*x;256]}

s7a: {
 i:.'","\*x  / input
 t:|/i  / max (top)
 l:i{+/{$[x<0;-x;x]}'x-y}/:!1+t / fuel
 &/l}
s7b: {
 i:.'","\*x  / input
 t:|/i  / max (top)
 l:i{+/{+/1+!$[x<0;-x;x]}'x-y}/:!1+t / fuel
 &/l}

s8a: +/{+/~^2 3 4 7?#'" "\*1_x}'" | "\'
/ seven ^ one -> a 
/ numbers -> b e f
/ one ^ f -> c
/ four ^ bcf -> d
/s8bi: {
/  (l;r):" "\'" | "\x
/  n:#'=,/l
/  G:{*{x=#'y}[y]#x}l
/  C:{*({z;x=y}[y;.x])#!x}@n
/  N:"abcefg";"cf";"acdeg";"acdfg";"bcdf";"abdfg";"abdefg";"acf";"abcdefg";"abcdfg"
/  10/N?v}
/s8b: s8bi'
f:{{(v;i):y;&/v<9^'x[i+1 -1]}[x]'+(x;!#x)}
s9a: {;{(#x)++/x}t ./:+&{(f@'x)&+f@'+x}@t:.''x}

/ f:{{(v;i):y;&/v>9^'x[i+1 -1]}[x]'+(x;!#x)}
/ g:{{(v;i):z;&/(~v=9)&9=0^x[i+y]}[x;y]'+(x;!#x)}
/ e:(({g[x;1]}@'t)|{g[x;-1]}@'t)&+(({g[x;1]}@'+t)|{g[x;-1]}@'+t)
/ r:e{.[x;y;:;0N]}/+&t=9
/ p:{{(v;i):y;s:+/0^'x[i+1 -1];$[e[i]&v<s;s;x@i]}[x]'+(x;!#x)}
/ i:{(y*~x)+x*(p@'y)++p@'+y}
/ I:i[(~e)&~^r]
/ / s:{(p@'x)+(x=0)*+p@'+x}/r
/ s ./:+&{(f@'x)&+f@'+x}

d: "([{<"!")]}>"
s: ")]}>"!3 57 1197 25137
s10a: +/0^{s@*x}'(() {$[32=d@*x;x;32=d y;$[y=d@*|x;-1_x;y];x,y]}/)'

s13a: {
 e:x?""
 (a;i):"="\*|" "\x[1+e]
 m:.'x[!e]
 r:$[*a="x";m;|'m]
 #={(a;b):y;($[a>x;(2*x)-a;a];b)}[.i]'r}
s13b: {
 m:.'x[!x?""]
 i:{"="\*|" "\x}'{~^x?\:"f"}#x
 a:m{
  (a;i):y
  s:*a="x"
  r:$[s;x;|'x]
  t:{(a;b):y;($[a>x;(2*x)-a;a];b)}[.i]'r
  $[s;t;|'t]}/i
 X:|/*'a
 Y:|/*'|'a
 +((1+X,Y)#" "){.[x;y;:;"#"]}/a}

s14a: {
 i:*x
 m:!/+" -> "\'2_x
 {(|/x)-&/x}@.#'=10{(*y),,/{(x y),*|y}[x]'2':y}[m]/i}
s14b: {
 i:*x
 m:!/+" -> "\'2_x
 f:{[m;x] +/,/(!x){((a;b);m):(y;*x y);(((,a,m)!z);((,m,b)!z))}[m]'.x}
 r:40(f[m])/#'=2':i
 s:+(+((*i),1)),{(1_*x),*|x}'+{((!x);.x)}@r
 *{(|/x)-&/x}@1_'+g@s}

s15: {-1+*|*|x{(r;c):y;.[x;y;+;(9999^x[r;c-1])&9999^x[r-1;c]]}/1_+!2##x}
s15a: {s15@.''x}
s15b: {
 w:5*#x
 t:w#w#'.''x
 i:+!2##t
 s15@t{.[x;y;{{$[0=x;9;x]}@9!x+y};+/-10!y]}/i}

C:{,/(4#2)\'"0123456789ABCDEF"?x}
t:{(y#x;y_x)}
T:{(a;b):(!0;x){(a;i):x;(n;r):t[i;y];(a,,n;r)}/y;a,,b}
L:{
 (u;v;r):{*x}{(u;a;r):x;(c;i;R):T[r;1 4];(*c;(a,i);R)}/(1;!0;x)
 (2/v;6+5*-4!#v;r)}
Pa:{
 (v;p;r):T[x;3 3]
 (v;p):2/'(v;p)
 `v`a`l`r!$[4=p
   (v,v,1_L r)
   [(l;r):T[r;1]
    $[*l
      [(n;r):t[r;11];(l;a;r):(2/n){(l;a;r):x;m:Pa@r;(l+m`l;a+m`a;m`r)}/(0;0;r);(v;v+a;18+l;r)]
      [(n;r):t[r;15];(u;a;r):{0<*x}{(n;a;r):x;m:Pa@r;(n-m`l;a+m`a;m`r)}/(2/n;0;r);(v;v+a;22+2/n;r)]]]]}
Pb:{
 (v;p;r):T[x;3 3]
 (v;p):2/'(v;p)
 `v`a`l`r!$[4=p
   (v,v,1_L r)
   [(l;r):T[r;1]
    $[*l
      [(n;r):t[r;11];(l;a;r):(2/n){(l;a;r):x;m:Pb@r;(l+m`l;a+m`a;m`r)}/(0;0;r);(v;v+a;18+l;r)]
      [(n;r):t[r;15];(u;a;r):{0<*x}{(n;a;r):x;m:Pb@r;(n-m`l;a+m`a;m`r)}/(2/n;0;r);(v;v+a;22+2/n;r)]]]]}

s16a: {(Pa@C x)`a}
/ s16a "8A004A801A8002F478"
/ s16a "620080001611562C8802118E34"
/ s16a "C0015000016115A2E0802F182340"
s16a (*(0:`in16))

F:{$[`i=@x;x;,/F'x]}
P:_`j?
D:{{$[`i=@x;0;1+o'x]}[x]-1}
X:{i:y?4;x:$[i>0;.[x;i-1;x[i]+];x];x:$[i<-2+#x;.[x;i+2;x[i+1]+];x];(?[x;i,i+2;0];?[y;i,i+2;y[i+1]-1])}
s18a: {}

s20a: {
 x:"#"='x
 r:*x
 i:8(+|0,)/2_x
 b:2{
  \y
  I:((&/0=(0,-2+#y)')')#+!2##y
  8(+|0,)/(2#-2+#y)#{(r;c):z;x@2/,/(3#c_)'3#r_y}[x;y]'I}[r]/i
 +// \b}

