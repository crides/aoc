\d s1
s: {$[y;x+y;0]}\.:' / hacky split and sum
a: |/s@
b: {s:s@x;m:(*|s),{$[x;0;y]}':s;+/3#m@>m}

\d s2
a: +/{(a;w;b):x-"@ W";b+3 6 0@3!b-a}'
b: +/{(a;w;b):x-"A X";1+(3*b)+3!b+a-1}'

\d s3
p: +/{x-$[x>"a";"`";"&"]}' / priority sum
c: #'=,/?:' / group and count
a: p@{g:c(2,-2!#x)#x;*(!g)@&.g=2}'
b: {p@{g:c@x;*(!g)@&.g=3}'((-3!#x),3)#x}

\d s4
p: .'''"-"\''","\'  / parse
i: {[x;y;z;w](x>z-1)&y<w+1} / contains in
r: {x+!1+y-x} / inclusive range
a: +/{|/i.'(x,y;y,x)}.'p@
b: +/~^{(.#'=,/r.'x)?2}'p@

\d s5
p: {i:1+4*!-4!1+#*x;s:((d:*&""~/:x)-1)#x;((~" "=)#'+s@\:i;{.'(" "\x)@1 3 5}'(1+d)_x)}
a: {(s;i):p@x;*'s{(n;a;b):y;d:|n#x@a-1;@[@[x;a-1;n_];b-1;d,]}/i}
b: {(s;i):p@x;*'s{(n;a;b):y;d:n#x@a-1;@[@[x;a-1;n_];b-1;d,]}/i}

\d s6
s: {x+*&{x=+/1=.#'=y}[x]'m:x':*y}
a: s@4
b: s@14

\d s7
p: {{$["$"=*x;,5_x;" "\x]}'({("$ ls"~x)|"dir"~3#x}')_1_x} / parse
t: *|(();()) {(d;l):x;$[1=#y;($[".."~*y;-1_d;d,,*y];l);(d;l,,(.*y;d,1_y))]}/p@ / traverse
s: {d:D@>#'D:({0<#x}')#?,/{(!1-#l)_\:l:*|x}'f:t x;*''(f;()){(f;d):x;s:+/*'({x~-1_*|y}[y]')#f,d;(f;d,,(s;y))}/d}
a: +/(100000>)#*|s@
b: {T:+/*D:s x;&/((T-40000000)<)#*|D}

\d s8
r: {|'x}
m: {|\-1^(:':x)}'
s: {((x y);(r x r y);(+r x r@+y);(+x@+y))}
a: {+/+/x>&/s[m;x]}
v: {(,()),-1_(){y,(y<1+)#x}\x}'
b: {*/#'''x{$[y;(1+(-1+#y)^*&~x>y)#y;y]}''/:s[v;x]}

\d s9
v: {$[x<0;-x;x]} / absolute; looks like the graph
n: {$[x;(-v@x)!x;0]} / signum
p: {,/({x:*x;(.y)#,$["R"~x;(1;0);"L"~x;(-1;0);"U"~x;(0;1);"D"~x;(0;-1);`]}.)'" "\'x} / parse
m: {$[&/2>v'x;0 0;n'x]} / moves
s: {#?*|(x#,0 0;()){(r;l):y;r:r{@[y;z;+;$[z;m@y[z-1]-y@z;x]]}[z]/!x;(r;l,-1#r)}[x]/p@y}
a: s@2
b: s@10
/ a: {#?*|((0;0);(0;0);()) {(h;t;l):x;h:y+h;t:t+m[h-t];(h;t;l,,t)}/p@x}

\d s10
e: {1+\,/{$[5>#x;0;(0;.5_x)]}'x}
a: {+/(2+m)*(e@x)@m:18+40*!6}
b: ".#"@6 40#(,/6#,!40){0=(-1 2)'y-x}'1,-1_e@

\d .
