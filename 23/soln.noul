d1a := (\inp ->
    inp then lines map (\l -> l search_all(R"\d")) map (\l -> int(l[0] $ l[-1])) then sum
);

d1b := (\inp -> (
    L := ["one", "two", "three", "four", "five", "six", "seven", "eight", "nine"];
    fpat := R"\d|" $ (L join "|");
    bpat := R".*(\d" $ (L map ("|"$) join "") $ ")";
    inp then lines map (\l -> (
        front := l search fpat;
        back := (l search bpat) !! 1;
        l = (front .. back) map (\i-> if (i =~ R"\d") i else $((L locate i) + 1)) join "";
        int(l)
    )) then sum
));

d2p := (\inp -> (
    col_color := (\c, g -> g map (\s -> (s !? c) coalesce 0) then max);
    inp then lines
    map (\l ->
        l[5:] split ": "
        then (int *** (\g ->
            g split "; " map (\s ->
                s split ", " map (\c ->
                    c split " " then (int *** id) then reverse) then dict)
            then ("red"(col_color) &&& "green"(col_color) &&& "blue"(col_color)) )))
));

d2a := (\inp -> d2p(inp) filter (\p -> all(p[1] zip (12 to 14) with (<))) map first then sum);
d2b := (\inp -> d2p(inp) map (\p -> product(p[1])) then sum);

d3p := (\inp, cond -> (
    ls := lines(inp);
    li := (\l -> (
        infs := [];
        o := 0;
        while (1) (
            n := l search R"\d+";
            if (n == null) return infs;
            i := l locate n;
            infs +.= [int(n), i + o, i + o + len(n)];
            l = l[i + len(n):];
            o = i + len(n) + o;
        );
    ));
    lis := enumerate(ls) map (\[i, l] -> li(l) map (\inf -> [inf[0], i, inf[1], inf[2]])) then flatten;
    sli := (\l -> enumerate(list(l)) filter (\p -> cond(p[1])));
    slis := ls map sli then enumerate map (\[i, infs] -> infs map (\[j, s] -> [i, j])) then flatten;
    [ls, lis, slis]
));

d3a := (\inp -> (
    ls, lis, slis := d3p(inp, (\c -> c != '.' and (c < '0' or c > '9')));
    sis := slis map (\c -> [-1, 0, 1] ^^ 2 map (\o -> vector(c) + vector(o))) then flatten;
    lis filter (\linf -> sis any (\c -> c[0] == linf[1] and linf[2] <= c[1] < linf[3])) map first then sum
));

d3b := (\inp -> (
    ls, lis, slis := d3p(inp, (== '*'));
    slis map (\sl -> (
        [-1, 0, 1] ^^ 2 map (\o -> vector(sl) + vector(o)) map (\c -> lis filter (\linf -> c[0] == linf[1] and linf[2] <= c[1] < linf[3])) then flatten then set
    ))
    filter (\i -> len(i) == 2) then map (\ns -> ns map first then product) then sum
));

d4p := (\inp -> (
    lines(inp) map (\l -> l split ": " then second then (\s -> s split " | " map (\g -> g search_all R"\d+" then set) fold &&))
));
d4a := (\inp -> d4p(inp) map (\l -> floor(2 ^ (len(l) - 1))) then sum);
d4b := (\inp -> (
    cis := enumerate(d4p(inp)) map (\[i, l] -> 1 to len(l) map (+i));
    mult := cis map (const 1);
    for (i, ci <<- cis) for (j <- ci) mult[j] += mult[i];
    sum(mult)
));

d5p := (\inp -> (
    ps := inp split "\n\n";
    seeds := ps[0] split ": " then second split " " map int;
    maps := ps[1:] map (\m -> lines(m)[1:] map (\l -> l split " " map int));
    [seeds, maps]
));
d5a := (\inp -> (
    seeds, maps := d5p(inp);
    for (m <- maps) (
        seeds = seeds map (\s -> (
            for ([t, b, l] <- m) if (b <= s < (b + l)) return s - b + t;
            s
        ));
    );
    min(seeds)
));
d5b := (\inp -> (
    seeds, maps := d5p(inp);
    maps fold (\ss, m -> (
        ss map (\ir -> (
            m fold (\[rest, next], [t, b, l] -> (
                new_rest, new_next := rest map (\[s, sl] -> 
                    if ((s + sl) <= b or s >= (b + l)) (
                        [[[s, sl]], []]
                    ) else (
                        ins, ine := max(s, b), min(s + sl, b + l);
                        ((if (s < b) [[s, b - s]] else []) ++
                            (if ((s + sl) > (b + l)) [[b + l, s + sl - b - l]] else [])) ..
                        [[ins - b + t, ine - ins]]
                    )
                ) then transpose map flatten or [[], []];
                [new_rest, next ++ new_next]
            )) from [[ir], []] fold ++
        )) then flatten
    )) from (seeds group 2) map first then min
));

d6a := (\inp ->
    lines(inp) map (\l -> l search_all(R"\d+") map int) then transpose
    map (\[t, dist] -> (1 til t) map (\i -> i * (t - i) > dist) then sum) then product
);
# (time - x) * x > dist
# => time * x - x^2 > dist
# => x^2 - time * x + dist < 0
# get roots: (-time +- sqrt(time^2 - 4 * dist)) / 2
d6b := (\inp -> (
    tim, dist := lines(inp) map (\l -> l split ": " then second replace R"\s+" with "" then int);
    ((V(-1, 1) * sqrt(tim ^ 2 - 4 * dist) - tim) / 2 fold - then abs then floor) + 1
));

d7_rank := (\proc, order -> \[card, b] -> (
    v := proc(frequencies(card));
    [-(len(v)), v[-1]] ++ (card map (order locate _))
));
d7s := (\order, proc -> \inp -> (
    cards := lines(inp) map (\l -> l split(" ") then (id *** int));
    cards sort (<=> on d7_rank(proc, order)) then enumerate map (\[i, [c, b]] -> (i + 1) * b) then sum
));
d7a := d7s("23456789TJQKA", \freq -> sort(values(freq)));
d7b := d7s("J23456789TQKA", \freq -> (
    v := sort(values(freq -. "J"));
    if (len(v) == 0) [5] else v[:-1] +. (v[-1] + freq["J"])
));

d8p := (\inp -> (
    ls := lines(inp);
    insts := ls[0];
    net := ls[2:] map (_ replace "[()]" with "" split " = " then (id *** (_ split ", "))) then dict;
    [insts, net]
));
d8a := (\inp -> (
    insts, net := d8p(inp);
    node := "AAA";
    for (i, inst <<- cycle(insts)) (
        node = net[node][inst == "R"];
        if (node == "ZZZ") return i + 1;
    )
));
d8b := (\inp -> (
    insts, net := d8p(inp);
    steps := (\node -> (
        for (i, inst <<- cycle(insts)) (
            node = net[node][inst == "R"];
            if (node ends_with "Z") return i + 1;
        )
    ));
    nodes := keys(net) filter (_ ends_with "A");
    nodes map steps fold lcm
));

d9p := (\inp ->
    lines(inp) map (\l -> l split " " map int)
    map (\seq -> (
        preds := [seq];
        while (1) (
            preds +.= preds[-1] pairwise flip(-);
            if (preds[-1] all (==0)) return preds;
        )
    ))
);
d9a := (\inp -> d9p(inp) map (\preds -> preds map last then sum) then sum);
d9b := (\inp -> d9p(inp) map (\preds -> preds map first then reverse fold (\s, i -> i - s)) then sum);

d10a := (\inp -> (
    net := lines(inp) map (\l -> l map (\c ->
        if (c == "F") {0, 3}
        else if (c == "J") {1, 2}
        else if (c == "|") {1, 3}
        else if (c == "-") {0, 2}
        else if (c == "7") {2, 3}
        else if (c == "L") {0, 1}
        else null
    ));
    start := enumerate(lines(inp)) find (\[i, l] -> (l locate? "S") != null) then (id *** (_ locate "S")) then vector;
    # print(start);
    net_get := (\[r, c] -> if (0 <= c < len(net[0]) and 0 <= r < len(net)) net[r][c] else null);
    dists := net map (\l -> l map const(10000));
    off_dirs := [[V(0, 1), 0], [V(1, 0), 3], [V(0, -1), 2], [V(-1, 0), 1]];
    start_dirs := off_dirs filter (\[d, cur_dir] -> (
        next := net_get(start + d);
        (next != null and ((cur_dir + 2) % 4) in next)
    ));
    # print(start_dirs);
    for ([_, start_dir] <- start_dirs) (
        cur := start;
        cur_dir := start_dir;
        dist := 0;
        while (1) (
            off := off_dirs find (\[o, d] -> d == cur_dir) then first;
            next_pos := cur + off;
            dists[cur[0]][cur[1]] = min(dist, dists[cur[0]][cur[1]]);
            next := net_get(next_pos);
            if (next == null) break;
            # print(cur, next_pos, cur_dir, next);
            cur_dir = only(list(next -. ((cur_dir + 2) % 4)));
            cur = next_pos;
            dist += 1;
            if (cur == start) break;
        );
    );
    # print(dists);
    flatten(dists) reject (==10000) then max
));
d10print_net := (\n -> print(n map (\l -> l map ($) join("")) join "\n"));

d10b := (\inp -> (
    mmap := lines(inp);
    net := lines(inp) map (\l -> l map (\c ->
        if (c == "F") {0, 3}
        else if (c == "J") {1, 2}
        else if (c == "|") {1, 3}
        else if (c == "-") {0, 2}
        else if (c == "7") {2, 3}
        else if (c == "L") {0, 1}
        else null
    ));
    start := enumerate(lines(inp)) find (\[i, l] -> (l locate? "S") != null) then (id *** (_ locate "S")) then vector;
    # print(start);
    net_get := (\[r, c] -> if (0 <= c < len(net[0]) and 0 <= r < len(net)) net[r][c] else null);
    off_dirs := [[V(0, 1), 0], [V(1, 0), 3], [V(0, -1), 2], [V(-1, 0), 1]];
    start_dirs := off_dirs filter (\[d, cur_dir] -> (
        next := net_get(start + d);
        (next != null and ((cur_dir + 2) % 4) in next)
    ));
    # print(start_dirs);
    net[start[0]][start[1]] = start_dirs map second then set;
    loop := {};
    for ([_, start_dir] <- start_dirs) (
        cur := start;
        cur_dir := start_dir;
        while (1) (
            loop |.= cur;
            off := off_dirs find (\[o, d] -> d == cur_dir) then first;
            next_pos := cur + off;
            next := net_get(next_pos);
            if (next == null) break;
            # print(cur, next_pos, cur_dir, next);
            cur_dir = only(list(next -. ((cur_dir + 2) % 4)));
            cur = next_pos;
            if (cur == start) break;
        );
    );
    rows, cols := len(net), len(net[0]);
    for (r <- (0 til rows)) (
        for (c <- (0 til cols)) (
            if (V(r, c) not_in loop) (
                net[r][c] = null;
                mmap[r][c] = ".";
            )
        )
    );
    print(mmap join "\n");
    seged := net map (\l -> l map const(0));

    cur := start;
    cur_dir := start_dirs[0][1];
    print(cur, cur_dir);
    wall := 0;
    while (1) (
        off := off_dirs find (\[o, d] -> d == cur_dir) then first;
        cur_tail := only(list(net_get(cur) -. cur_dir));
        rights := cur_dir iterate (\d -> if (d == cur_tail) break else (d - 1) %% 4);
        lefts := cur_dir iterate (\d -> if (d == cur_tail) break else (d + 1) %% 4);
        for (right <- rights) (
            right_off := off_dirs find (\[o, d] -> d == right) then first;
            ray := cur + right_off;
            while (net_get(ray) == null) (
                if (cur in [V(5, 10), V(5, 14)]) print("right ray", ray);
                if (0 <= ray[0] < rows and 0 <= ray[1] < cols) (
                    seged[ray[0]][ray[1]] = 2;
                    ray += right_off;
                ) else (
                    if (wall == 0 or wall == 1) (
                        wall = 1;
                    ) else (
                        print("!!!!!!!!!!!!!!!!!!!!!!!!!");
                    );
                    break;
                )
            );
        );

        for (left <- lefts) (
            left_off := off_dirs find (\[o, d] -> d == left) then first;
            left_ray := cur + left_off;
            while (net_get(left_ray) == null) (
                if (0 <= left_ray[0] < rows and 0 <= left_ray[1] < cols) (
                    seged[left_ray[0]][left_ray[1]] = 1;
                    left_ray += left_off;
                ) else (
                    if (wall == 0 or wall == 2) (
                        wall = 2;
                    ) else (
                        print("!!!!!!!!!!!!!!!!!!!!!!!!!");
                    );
                    break;
                )
            );
        );
        next_pos := cur + off;
        next := net_get(next_pos);
        cur_dir = only(list(next -. ((cur_dir + 2) % 4)));
        cur = next_pos;
        if (cur == start) break;
    );
    d10print_net(seged);
    flatten(seged) count (==wall)
));
