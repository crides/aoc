# vim: set guifont=Uiua386\:h24:
# Experimental!

┌─╴D₁
  P ← °⊟⍉⊜(⊜⋕⊸≠@\s)⊸≠@\n
  A ← /+⌵-∩⍆P
  B ← /+≡(×⟜(⧻⊚⌕))⊙¤P
└─╴

┌─╴D₂
  P    ← ⊜(□⊜⋕⊸≠@\s)⊸≠@\n
  Safe ← ∨∩(/××⊃≤₃≥₁)⟜¯≡/-⧈∘2
  A    ← ⧻⊚≡(Safe°□)P
  B    ← ⧻⊚≡(/∨≡(Safe⍜↻↘₁)⊙¤°⊏°□)P
└─╴

┌─╴D₃
  A ← /+≡(◇×∩⋕°⊟↘1)regex$ mul\((\d+),(\d+)\)
  B ← A▽⊸(⬚1\(⨬(⊙◌|⋅∘)±,)+⊙×₂∩⌕"do()","don't()")
└─╴

┌─╴D₄
  Search ← /+≡(+∩(/+⌕)⇌,,"XMAS")
  Diag   ← ≡↻¯°⊏≡⊂⟜(¤▽:@\s-1⧻)
  A      ← /+[⊃(Search⍉Diag|Search⍉⍜⇌Diag|Search|Search⍉)]⊜∘⊸≠@\n
  B ← (
    ⊃(Diag|⍜⇌Diag)⊜∘⊸≠@\n
    ∩(⍜⍉(∨∩(↻¯1⌕)⇌,,"MAS"))
    /+♭×⊓(≡↻₂≡↻°⊏|⍜⇌(≡↻°⊏)) # ↻2 is.. probably due to the ⇌, but is added by experiment
  )
└─╴

┌─╴D₅
  P ← ⊓(⊜(⊜⋕⊸≠@|)⊸≠@\n|⊜(□⊜⋕⊸≠@,)⊸≠@\n)°□₂⊜□⊸(¬⦷)"\n\n"
  A ← /+≡◇(×⊃(/×≡/>▽⊸(/×⍉±)+1⬚¯1⊗|⊡⊸(⌊÷2⧻)◌))¤P
  B ← /+≡◇(
    /×≡/>∩▽◡(,⟜(/×⍉±)+1⬚¯1⊗)
    ⨬(⊡⊸(⌊÷2⧻)⊏⊸(⍏/+⊞=)⊣⍉|⋅0)
  )¤P
└─╴

┌─╴D₆
  P ← ⊃(⊢⊚⌕@^|=@#)⊜∘⊸≠@\n
  # looped been ? dir pos map
  Exp ← ⋅⊙⋅⋅⊙◌⍢(|6.6
    ◡(⊃⊡∘+⊙⊙⋅∘⊙⊙)◌◌ # nextloc next dir pos been map ? cond looped dir pos been map
    ⨬(:⊙⊙◌|⋅⍜(ℂ°⊟)(×i))
    ◡(⊸(¬∨)⊃(∨∩/∨⊃(<0|≤)⊃(+⊙∘|△⋅⋅⋅∘)|⬚0⊢∊:¤⊟))
    ⊙⊙⊃(⊙∘|⊂:¤⊟)
  ).1 1⊙⊙(°△0_2_2)
  A ← +1/↥⊛↘0_1°0Exp¯1_0P
  B ← (
    ◴⊃(♭₂↘1_1°0Exp¯1_0|∩¤)P
    ⧻◴▽∩≡°□°⊟⍉wait≡pool(∩□⟜(⊙◌Exp¯1_0⊙(⍜⊡⋅1):))
  )
  # A ← ⋅⋅⊓(/+♭)◌⍢(|4.4
  #   ◡(⊃⊡∘+⊙⊙⋅∘) # nextloc next dir pos been map ? dir pos been map
  #   ⊙(⟜⍜⊡⋅1)⨬(:⊙⊙◌|⋅⍜(ℂ°⊟)(×i))
  # )◡(|3
  #   ¬∨∩/∨⊃(<0|≤)⊃(+⊙∘|△⋅⋅∘)
  # )¯1_0⊙⊸(∵⋅0)P
  # Exp‼ ← |4 ⋅⋅⊙◌⍢(
  #   # search mul
  #   ◡(⊃⊡∘+⊙⊙⋅∘) # nextloc next dir pos been map ? dir pos been map
  #   ⊃(⊙∘|⊂:¤⊟)⨬(
  #     :⊙⊙◌
  #   | ⟜+⋅⍜(ℂ°⊟)(×^1)
  #     ⍥(⊃(⋅⊙∘|⊂⊙⋅⋅∘)◡(Exp‼(0)(i)?₂⊙⊙⋅∘⊃(⊙∘|¤⊟)¯))^0
  #   )
  # )◡(
  #   ¬∨∩/∨⊃(<0|≤)⊃(+⊙∘|△⋅⋅⋅∘)
  # )
  # B ← (
  #   ?⊂⊃(⍜(↙∞_1)¯Exp‼(0)(i)1_0⊙[]|Exp‼(1)(¯i)⊃(⊙∘|¤⊟)¯1_0)P
  #   ⧻◴▽≡((×⊓(=i÷∩(ℂ°⊟)|⊸≍)⊙:∩°⊟°⊟))⊏⧅<2°⊏
  # )
└─╴

┌─╴D₇
  P  ← ⊜(⊓⋕(□⊜⋕⊸≠@\s)°$"_: _")⊸≠@\n
  S! ← /+wait≡pool(×⊸∊◇/^0):P
  A  ← S!(⊂⊃+×)
  B  ← S!(⊂⊂⊃(+|×|+⊸(×⊙⍜ₙ₁₀(+1⌊))))
└─╴

┌─╴D₈
  P ← ⊛⊃(/◇⊂≡°◇°▽|⊚|¤△)⊸(≠@.)⊜∘⊸≠@\n
  A ← ⧻◴▽:⟜(≡/××⊃≥₀>)/◇⊂⊕(□≡(-⊙×₂°⊟)⧅(¬≍)2)P
  B ← (
    /◇⊂⊕(□/◇⊂≡(□-×⇡48∩¤⤚-°⊟)⧅(¬≍)2)P
    ⧻◴▽:⟜(≡/××⊃≥₀>)
  )
└─╴

┌─╴D₉
  # Show ← &p⊏:".0123456789"+1▽:°⊟⍉
  P ← °⊟⍉-@0↯∞_2⊂:@0▽⊸≠@\n
  P ← ?▽∵⋕:⊏:-1⇡+1⊸/↥⍜▽\+.¬◿2°⊏▽⊸≠@\n
  A ← (
    /◇⊂♭⍉:⊙⊟⊓(⊃(/+|⍚▽:°⊏)|⍚▽:¯1)P
    ⊙◌⍢(▽⊸(⍜⇌\↥≠¯1)⍜⊏◌⊙(⊃↘↙¯1)⊸(⊢⊚⌕¯1))(≠⧻)
    /+×°⊏
  )
  A ← /+×°⊏⍢(▽⍜⇌\↥⊸≠¯1⍜⊡◌⊸⊗¯1:⍜⇌°⊂|/↥=¯1)P
  B ← (
    ▽⊸(≠0⊣⍉)♭₂⤸1⊟⊓(⍉⊟°⊏|⍉⊂¯1¤)P
    ∧(⊃(⊣⊏⊙⋅∘|⊙⊙∘)⊃(⊢⊚=⊙(⊢⍉)|⊙∘)  # file-size file-ind file-id fs ? file-id fs
      ⊃(⬚¯1⊢⊚×⊓=₋₁≤°⊟⍉:⊙⋅⋅∘|⊙⊙⊙∘) # space-ind file-size file-ind file-id fs ? file-size file-ind file-id fs
      ⊃(⊙⋅⊙⋅∘|⋅⊙⋅∘)               # space-ind file-ind fs file-size file-id
      ⨬((⊙(⍜(⊢⊏)⋅¯1)              # space-ind fs space-size file-size file-id ? space-ind file-ind fs space-size file-size file-id
         (⍜⊏(?⊟⊙(⊟¯1)⊟?:⊙:⟜-:?⊣)) # fs ? space-ind fs file-size file-id
        )
      | ⋅⋅⊙⋅◌
      )∨◡⊃(<|=¯1)
      ⍉⊟:°▽▽:°⊟⍉
    )⊸(⇌+1⇡/↥⊢⍉)
    /+▽≠¯1⤙×°⊏▽:°⊟⍉
  )
  B ← (
    /+×°⊏↥0 ∧(
      ⊙,∩▽,¯1⧻⊚◡=
      ∩⊚⊓⌕◡⌕
      ⍣(°1⤚>∩⊢
        ⍜⊙↘(⊂⊙↘:⊸⧻):⊙⟜(⍜▽∵⋅¯1⊸⦷)
      )⋅⋅◌
    ) ⇌⇡-1⧻⊸◴P
  )
└─╴

┌─╴D₁₀
  Neis ← [∩¯,,⇌.⇡2]
  S! ← /+≡(
    ⧻^0⊙◌path(
      ▽=⊃(+1⊡|⤚≡⬚¯1⊡+Neis∩¤)
    | =9⊡
    )
  )⊃(⊚=0)¤-@0⊜∘⊸≠@\n
  A ← S!(◴≡◇⊣)
  B ← S!(∘)
└─╴

┌─╴D₁₁
  # count ? n rem
  Blink ← |2 memo(
    ⍣(⍩(1◌)⊙°0
    | ⍩(Blink1-1)°0
    | ⍩(/+≡Blink⊙-₁[⊃(⌊÷|◿)]ⁿ:10÷2)°0⊸◿2⊸(+1⌊ₙ10)
    | Blink⊓×₂₀₂₄-₁))
  S ← /+≡Blink⊜⋕⊸≠@\s▽⊸≠@\n:
  A ← S 25
  B ← S 75
└─╴

┌─╴D₁₂
  P ← ⊜□⊃(-@\0|⇡△)⊜∘⊸≠@\n
  A ← /+≡◇(×⟜(-:×4)⊃(⧻|/+≡(=1/+⌵/-)⧅≠2))P
  B ← /+≡◇(
    ×⧻⟜(
      °⊚
      /+[(∩₄(/+≡(⧻⊜0.)⍉=1≡\(×⊸+))⊃(∘|⍉|≡⇌|≡⇌⍉))]
    )
  )P
└─╴

┌─╴D₁₃
  P ← ⊜(
    ▽⊸≠@\n
    ∩₃⊟∩₆⋕°$"Button A: X+_, Y+_Button B: X+_, Y+_Prize: X=_, Y=_"
  )¬⊸⦷"\n\n"
  A ← /+≡(
    ⊙(-⊙(¤:)⊸×¤⟜⊸(⇡↧100/↥⌊÷))
    ⬚0⊢+×3⊢⍉∩▽’×(⊃(/↧⍉=0◿|⊸(/=⍉)÷))¤
  )P
  # aA + cB = e
  # bA + dB = f
  # A = (e - cB) / a = (f - dB) / b
  # be - bcB = af - adB
  # B = (ad - bc) / (af - be)
  B ← /+♭▽⊸(/↧⍉=⊸⌊)≡(|3
    ⊙⊙⊙⊙(∩(+10000000000000))∩₃°⊟
    ◠(÷∩-∩₄×⊃(⋅⊙∘|⊙⋅⋅∘|⋅⊙⋅⋅∘|⊙⋅⋅⋅⋅∘))
    ⊟⊸(×3÷⊙(-×|:)⊙⋅⊙⋅⊙⋅∘)
  )P
└─╴

┌─╴D₁₄
  P ← ⊃(⊜(∩⊟∩₄⋕°$"p=_,_ v=_,_")⊸≠@\n|⨬(7_11|101_103)>1000⧻)
  A ← /×⊕⧻.⊛▽⊸(/↧≠0⍉)±-⊃(¤⌊÷2|◿¤):≡(+⊙×₁₀₀)P
  B ← ⋅⋅⋅∘⍢⊓(⊸+|⊙+₁)(<30/↥⊜⧻.◡(°⊚◿¤:⊙◌))⊙⊙⊙0P
└─╴

┌─╴D₁₅
  Dirs ← [¯1_0 0_1 1_0 0_¯1]
  P ← ⊓(
    ⊃(⊢⊚=@@|⊗:".O#"⍜♭(⍜▽∵⋅@.⊸=@@))⊜∘⊸≠@\n
  | ⊗:"^>v<"▽⊸≠@\n
  )°$"_\n\n_"
  Shove ← ⍜↙(⟜(∊:0)⍥(⍜⊏⇌⊸(⊟0⊗0))⊸(/↧∊:0_1))⊸(⊗2)
  Draw  ← &p/$"_\n_"⊏:".O#@"⍜⊡⋅3
  A ← (
    ∧(
      &p⊏:"^>v<".
      :⊙(+×)⊃(
        ⨬(⍉⍜⊏(⍜(⇌↙)Shove:)⊙:°⊟⊓⇌⍉   # up
        | ⍜⊏(⍜(↘)Shove:)⊙:⊙+₁°⊟     # right
        | ⍉⍜⊏(⍜(↘)Shove:)⊙:⊙+₁°⊟⊓⇌⍉ # down
        | ⍜⊏(⍜(⇌↙)Shove:)⊙:°⊟       # left
        )
      | ⊏:Dirs ⊙∘
      )
    )⤙⊙⊙◌P
    /++×100°⊟⍉⊚=1◌
  )
└─╴

┌─╴D₁₆
  Path ← path(
    ∩▽,⊃(+1×1000¬≡≍|⊃(≠@#≡⊡◌|≡⊟)⟜+⊙⋅⊙¤)D₁₅~Dirs⊃↙↘1
  | =@E⊡⊣
  )⊸(⊟0_1⊢⊚=@S)⊜∘⊸≠@\n
  A ← ◌Path
  B ← ⧻◴⊣⤸1/◇⊂⊙◌Path
└─╴

┌─╴D₁₇
  P     ← ⊓(⊜(⋕↘12)⊸≠@\n|⊜⋕⊸≠@,▽⊸≠@\n↘9)°$"_\n\n_"
  Combo ← ⨬(0|1|2|3|⊢,|⊡1,|⊣,)
  # ip reg out ? ip prog reg out
  Step ← (
    ⊃(∘|+×2≠3⊢|⋅⋅∘)⤚(↙2↘)
    # inst ip reg out
    °⊟
    ⨬(⍜⊙◌(⍜⊢(⌊÷ⁿ:2:):)Combo        # adv
    | ⍜⊙◌(⍜⊡₁(⍜∩⋯(⬚0≠)):)          # bxl
    | ⍜⊙◌(⍜⊡₁◌:◿8)Combo            # bst
    | ⨬(+2◌|⊙◌)◡(±⊢⋅⋅∘)            # jnz
    | ⍜◌(⍜⊡₁(⍜∩⋯(⬚0≠))⟜⊣)◌         # bxc
    | ⍜⊙⋅◌(⊂:◿8)Combo              # out
    | ⍜⊙◌(⍜⊡₁◌⤙(⌊÷⊃(ⁿ:2|⋅⊢)))Combo # bdv
    | ⍜⊙◌(⍜⊣◌⤙(⌊÷⊃(ⁿ:2|⋅⊢)))Combo  # cdv
    )
  )
  # reg out ? prog reg
  Exec ← ⋅◌⍢(:⊃⋅∘Step)(>⊙⧻)0⊙⊙[]
  A    ← /$"_,_"⋅∘Exec:P
  # res ? start target prog
  NextOctit ← ▽⊸(=⊙(≡(⋅⊢Exec⊙(⊂:0_0))¤))⊃(⋅⊙∘|+⇡8×8)
  B ← /↧⋅⊙⋅◌⍢(
    # Q [res] prog rev-s
    ⊃(⊸=◡⊃(⋅⋅⋅⋅⧻|⨬(1|⧻base8)⊸±)°⊂
      ⨬(◴⊂:⊃(⋅⋅∘|NextOctit⊃(⋅∘|⊡⊙⋅⋅⋅⋅∘|⋅⋅⋅⋅∘)|⋅⋅⋅∘)|⊙⊂:◌)
    | ⋅⋅⊙∘)
  )(>0⧻
  )◡(⊙[]NextOctit0 ⊢:)⊃(↘¯2|⇌)◌P
└─╴

┌─╴D₁₈
  A ← (
    0_0⬚0↙⊟.⊙(°⊚↙)⨬(7 12|71 1024)⊸(>1000⧻)⊜(⊜⋕⊸≠@,)⊸≠@\n
    ◌path(≡⊸1▽⤚(¬≡⬚1⊡)⊙¤+A₂¤|≍⊙(-1△))
  )
  B ← (
    ⬚1↙⊟.⊙(¬°⊚⊸↙₁)⨬(7|71)⊸(>1000⧻)⊜(⊜⋕⊸≠@,)⊸≠@\n
    $"_,_"°⊟⋅⊢⍢(⊃(⍜⊡⋅0⊏₁:|⋅↘₁))(
      # ∊:1≡◇(↧⊃(∊:0_0|∊))⊃(⊜□:°⊡|-1¤△)
      ⍣(1◌⊢path(▽⤚(≡⬚0⊡)⊙¤+A₂¤|≍⊙(-1△))0_0|0)
    )
  )
└─╴

┌─╴D₁₉
  P ← ⊓(⊜□¬⊸⦷", "|⊜□⊸≠@\n)°$"_\n\n_"
  # num ? pat list
  Fit! ← |2 memo(
    ^0/+≡(
      ⍣(⍩(1)⋅◌°.
      | ⍩(⨬(⋅⋅0|Fit!(^0)):⊙≍⊃↘↙)°1⊸<◡∩⧻
      | 0⋅⋅◌
      )∩°□
    )¤⊙⟜¤)
  A ← /+≡Fit!(>0):¤P
  B ← /+≡Fit!(∘):¤P
└─╴

┌─╴D₂₂
  RngNext ← ⬚0(↙24≠⟜°↘₁₁↙24≠⟜↘₅↙24≠⟜°↘₆)
  A ← /+wait≡pool⍜⋯⍥₂₀₀₀RngNext⊜⋕⊸≠@\n
  B ← /↥/+wait≡pool(
    ◿10⇌[⍥₁₉₉₉⊸⍜⋯RngNext]
    ⧈₅⊃(⌝base19+9⧈-|⊣)
    ∧(⍜⊡◌⊙:)⊓∩⇌(⊚130321)
  )⊜⋕⊸≠@\n
└─╴
