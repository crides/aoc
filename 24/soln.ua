# vim: set guifont=Uiua386\:h24:
# Experimental!

┌─╴D₁
  P ← °⊟⍉⊜(⊜⋕⊸≠@\s)⊸≠@\n
  A ← /+⌵-∩⍆P
  B ← /+≡(×⟜(⧻⊚⌕))⊙¤P
└─╴

┌─╴D₂
  P    ← ⊜(□⊜⋕⊸≠@\s)⊸≠@\n
  Safe ← ∨∩(/××⊃≤₃≥₁)⟜¯≡/-◫2
  A    ← ⧻⊚≡(Safe°□)P
  B    ← ⧻⊚≡(/∨≡(Safe⍜↻↘₁)⊙¤°⊏°□)P
└─╴

┌─╴D₃
  A ← /+≡(◇×∩⋕°⊟↘1)regex$ mul\((\d+),(\d+)\)
  B ← A▽⊸(⬚1\(⨬(⊙◌|⋅∘)±,)+⊙×₂∩⌕"do()","don't()")
└─╴

┌─╴D₄
  Search ← /+≡(+∩(/+⌕)⇌,,"XMAS")
  Diag   ← ≡↻¯°⊏≡⊂⟜(¤▽:@\s-1⧻)
  A      ← /+[⊃(Search⍉Diag|Search⍉⍜⇌Diag|Search|Search⍉)]⊜∘⊸≠@\n
  B ← (
    ⊃(Diag|⍜⇌Diag)⊜∘⊸≠@\n
    ∩(⍜⍉(∨∩(↻¯1⌕)⇌,,"MAS"))
    /+♭×⊓(≡↻₂≡↻°⊏|⍜⇌(≡↻°⊏)) # ↻2 is.. probably due to the ⇌, but is added by experiment
  )
└─╴

┌─╴D₅
  P ← ⊓(⊜(⊜⋕⊸≠@|)⊸≠@\n|⊜(□⊜⋕⊸≠@,)⊸≠@\n)°□₂⊜□⊸(¬⦷)"\n\n"
  A ← /+≡◇(×⊃(/×≡/>▽⊸(/×⍉±)+1⬚¯1⊗|⊡⊸(⌊÷2⧻)◌))¤P
  B ← /+≡◇(
    /×≡/>∩▽◡(,⟜(/×⍉±)+1⬚¯1⊗)
    ⨬(⊡⊸(⌊÷2⧻)⊏⊸(⍏/+⊞=)⊣⍉|⋅0)
  )¤P
└─╴

┌─╴D₆
  P ← ⊃(⊢⊚⌕@^|=@#)⊜∘⊸≠@\n
  # looped been ? dir pos map
  Exp ← ⋅⊙⋅⋅⊙◌⍢(|6.6
    ◡(⊃⊡∘+⊙⊙⋅∘⊙⊙)◌◌ # nextloc next dir pos been map ? cond looped dir pos been map
    ⨬(:⊙⊙◌|⋅⍜(ℂ°⊟)(×i))
    ◡(⊸(¬∨)⊃(∨∩/∨⊃(<0|≤)⊃(+⊙∘|△⋅⋅⋅∘)|⬚0⊢∈:¤⊟))
    ⊙⊙⊃(⊙∘|⊂:¤⊟)
  ).1 1⊙⊙(°△0_2_2)
  A ← +1/↥⊛↘0_1°0Exp¯1_0P
  B ← (
    ◴⊃(♭₂↘1_1°0Exp¯1_0|∩¤)P
    ⧻◴▽∩≡°□°⊟⍉wait≡pool(∩□⟜(⊙◌Exp¯1_0⊙(⍜⊡⋅1):))
  )
└─╴

┌─╴D₇
  P  ← ⊜(⊓⋕(□⊜⋕⊸≠@\s)°$"_: _")⊸≠@\n
  S! ← /+wait≡pool(×⊸∈◇/^0):P
  A  ← S!(⊂⊃+×)
  B  ← S!(⊂⊂⊃(+|×|+⊸(×⊙⍜ₙ₁₀(+1⌊))))
└─╴

┌─╴D₈
  P ← ⊛⊃(/◇⊂≡°◇°▽|⊚|¤△)⊸(≠@.)⊜∘⊸≠@\n
  A ← ⧻◴▽:⟜(≡/××⊃≥₀>)/◇⊂⊕(□≡(-⊙×₂°⊟)⧅(¬≍)2)P
  B ← (
    /◇⊂⊕(□/◇⊂≡(□-×⇡48∩¤⤚-°⊟)⧅(¬≍)2)P
    ⧻◴▽:⟜(≡/××⊃≥₀>)
  )
└─╴

┌─╴D₉
  Show ← &p⊏:".0123456789"+1▽:°⊟⍉
  P    ← °⊟⍉-@0↯∞_2⊂:@0▽⊸≠@\n
  A ← (
    /◇⊂♭⍉:⊙⊟⊓(⊃(/+|⍚▽:°⊏)|⍚▽:¯1)P
    ⊙◌⍢(▽⊸(⍜⇌\↥≠¯1)⍜⊏◌⊙(⊃↘↙¯1)⊸(⊢⊚⌕¯1))(≠⧻)
    /+×°⊏
  )
  B ← (
    ▽⊸(≠0⊣⍉)♭₂⤸1⊟⊓(⍉⊟°⊏|⍉⊂¯1¤)P
    ∧(⊃(⊣⊏⊙⋅∘|⊙⊙∘)⊃(⊢⊚=⊙(⊢⍉)|⊙∘)  # file-size file-ind file-id fs ? file-id fs
      ⊃(⬚¯1⊢⊚×⊓=₋₁≤°⊟⍉:⊙⋅⋅∘|⊙⊙⊙∘) # space-ind file-size file-ind file-id fs ? file-size file-ind file-id fs
      ⊃(⊙⋅⊙⋅∘|⋅⊙⋅∘)               # space-ind file-ind fs file-size file-id
      ⨬((⊙(⍜(⊢⊏)⋅¯1)              # space-ind fs space-size file-size file-id ? space-ind file-ind fs space-size file-size file-id
         (⍜⊏(?⊟⊙(⊟¯1)⊟?:⊙:⟜-:?⊣)) # fs ? space-ind fs file-size file-id
        )
      | ⋅⋅⊙⋅◌
      )∨◡⊃(<|=¯1)
      ⍉⊟:°▽▽:°⊟⍉
    )⊸(⇌+1⇡/↥⊢⍉)
    /+▽≠¯1⤙×°⊏▽:°⊟⍉
  )
└─╴

┌─╴D₁₀
  Neis ← [∩¯,,⇌.⇡2]
  S! ← /+≡(
    ⧻^0⊙◌astar(
      ▽=⊃(+1⊡|⤚≡⬚¯1⊡+Neis∩¤)
    | 0
    | =9⊡
    )
  )⊃(⊚=0)¤-@0⊜∘⊸≠@\n
  A ← S!(◴≡◇⊣)
  B ← S!(∘)
└─╴

┌─╴D₁₁
  # count ? n rem
  Blink ← |2 memo(
    ⍣(⍩(1◌)⊙°0
    | ⍩(Blink1-1)°0
    | ⍩(/+≡Blink⊙-₁[⊃(⌊÷|◿)]ⁿ:10÷2)°0⊸◿2⊸(+1⌊ₙ10)
    | Blink⊓×₂₀₂₄-₁))
  S ← /+≡Blink⊜⋕⊸≠@\s▽⊸≠@\n:
  A ← S 25
  B ← S 75
└─╴
